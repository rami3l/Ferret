import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'application'
    // lombok (generation of getters, setters, constructors, builders, etc.)
    id 'io.freefair.lombok' version '6.3.0'
}

ext {
    versions = [
            'junit': '5.8.2',
            'htsjdk': '2.24.1',
            'reactor': '3.4.15',
            'awaitility': '4.1.1'
    ]
    javaMainClass = "fr.ferret.FerretMain"
}

repositories {
    mavenCentral()
}

dependencies {
    // Library used to deal with vcf files
    // https://mvnrepository.com/artifact/com.github.samtools/htsjdk
    implementation(group: 'com.github.samtools', name: 'htsjdk', version: versions['htsjdk'])

    // Use reactor for asynchronous operations
    implementation(group: 'io.projectreactor', name: 'reactor-core', version: versions['reactor'])

    // Use JUnit Jupiter, reactor-test and awaitility for testing.
    testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter', version: versions['junit'])
    testImplementation(group: 'io.projectreactor', name: 'reactor-test', version: versions['reactor'])
    testImplementation(group: 'org.awaitility', name: 'awaitility', version: versions['awaitility'])
}

test {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

application {
    mainClass = javaMainClass
}

java {
    group = 'fr.ferret'
    version = '1.0-SNAPSHOT'
    description = 'NewFerret'
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
  manifest {
    attributes(
      'Main-Class': javaMainClass
    )
  }
}

// Tasks

defaultTasks 'build'

tasks.javadoc.doFirst {
    println 'Exporting javadoc under `./build/docs/javadoc`...'
}

tasks.build.doFirst {
    println 'Building...'
}

tasks.run.doFirst {
    println 'Running...'
}

tasks.test.doFirst {
    println 'Testing...'
}

tasks.jar.doFirst {
    println 'Generating jar under `./build/libs`'
}

task javadocOpen(type:Exec) {
    dependsOn 'javadoc'
    description 'Opens javadoc.'

    workingDir './build/docs/javadoc'

    // Let Linux be the default case.
    commandLine 'xdg-open', 'index.html'
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        workingDir System.getProperty("user.dir") + '/build/docs/javadoc'
        commandLine 'pwsh', '-Command', 'Invoke-Item index.html'
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        commandLine 'open', 'index.html'
    }
}
